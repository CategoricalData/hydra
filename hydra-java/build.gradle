plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id('io.github.gradle-nexus.publish-plugin') version '1.1.0'
    id 'checkstyle'
}

group 'net.fortytwo.hydra'
version '0.2.0'

// We use Java 8 for maximum compatibility with legacy systems, such as certain Spark deployments
sourceCompatibility = '1.8'
targetCompatibility = '1.8'

dependencies {
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
//  implementation 'com.linkedin.pegasus:generator:29.22.13'

    implementation group: 'com.cedarsoftware', name: 'json-io', version: '4.14.1'

    // We use RDF 3.x rather than 4.x or 5.x for compatibility with Java 8
    implementation 'org.eclipse.rdf4j:rdf4j-rio-ntriples:3.7.7'

    testImplementation(platform('org.junit:junit-bom:5.9.2'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.junit.jupiter:junit-jupiter-params:5.9.2')
}

sourceSets {
    main {
        java {
            srcDirs 'src/main/java'
            srcDirs 'src/gen-main/java'
        }
    }
    test {
        java {
            srcDirs 'src/test/java'
            srcDirs 'src/gen-test/java'
        }
    }
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "https://linkedin.jfrog.io/artifactory/open-source/" // For Pegasus dependencies
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'hydra'
                description = 'The Hydra language for strongly-typed transformations'
                url = 'https://github.com/CategoricalData/hydra'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'joshsh'
                        name = 'Joshua Shinavier'
                    }
                }
                scm {
                    url = 'https://github.com/CategoricalData/hydra'
                    connection = 'scm:git://github.com/ConnectedData/hydra.git'
                    developerConnection = 'scm:git://github.com/CategoricalData/hydra.git'
                }
            }
        }
    }

    // RESTORE ME
//    repositories {
//        mavenLocal()
//    }
}

nexusPublishing {
    repositories {
        sonatype()
    }
}

signing {
    sign publishing.publications.mavenJava
}

checkstyle {
    toolVersion = '8.29' // Temporary, due to the Java 1.8 limitation. A more recent version of Checkstyle it 10.12.4
}

tasks.withType(Checkstyle) {
    reports {
        xml.required = false
        html.required = true
    }
}

checkstyleMain {
    source = 'src/main'
    include '**/*.java'
}

checkstyleTest {
    source = 'src/test'
    include '**/*.java'
}
